/*

   Cathedral?

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_TROY
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BOS)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Troy00;
procedure Troy01;
procedure Troy02;
procedure Troy03;
procedure Troy04;
procedure Troy05;
procedure Troy06;
procedure Troy07;
procedure Troy08;

variable tmp_hostile;
variable Only_Once := 1;
variable TalkToPlayer;

variable weaponPtr;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_NECROPOLIS_MUTANT);
		set_self_ai(AI_SUPER_MUTANT_AGGRESSIVE);
		reg_anim_clear(self_obj);
		if (local_var(5) == 0) then begin
			set_local_var(5, 1);
			weaponPtr := create_object(PID_MINIGUN, 0, 0);
			add_obj_to_inven(self_obj, weaponPtr);
			wield_obj_critter(self_obj, weaponPtr);
			add_obj_to_inven(self_obj, create_object(PID_5MM_JHP, 0, 0));
		end
	end
end

procedure combat
begin
	tmp_hostile := 1;
end

procedure critter_p_proc
begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	if ((local_var(4) == 0) and (tile_distance_objs(self_obj, dude_obj) <= 5)) then begin
		set_local_var(4, 1);
		dialogue_system_enter;
	end
	if (local_var(4) == 0) then begin
		self_walk_to_tile(tile_num_in_direction(dude_tile, random(0, 5), 1));
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	get_reaction
	if (local_var(4) == 1) then begin
		start_gdialog(604, self_obj, 4, -1, -1);
		gsay_start;
		call Troy00;
		gsay_end;
		end_dialogue;
	end
	else begin
		display_msg(mstr(132));
	end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(mstr(100));
end

procedure Troy00
begin
	Reply(101);
	giq_option(7, NAME, 102, Troy01, NEUTRAL_REACTION);
	giq_option(4, NAME, 103, Troy01, NEUTRAL_REACTION);
	giq_option(4, NAME, 104, Troy02, NEUTRAL_REACTION);
	giq_option(4, NAME, 105, Troy03, BAD_REACTION);
	giq_option(4, NAME, 106, Troy04, BAD_REACTION);
	giq_option(-3, NAME, 107, Troy05, NEUTRAL_REACTION);
end

procedure Troy01
begin
	Reply(108);
	giq_option(4, NAME, 109, Troy06, NEUTRAL_REACTION);
	giq_option(4, NAME, 110, Troy02, NEUTRAL_REACTION);
	giq_option(4, NAME, 111, Troy03, BAD_REACTION);
	giq_option(4, NAME, 112, Troy07, BAD_REACTION);
end

procedure Troy02
begin
	NMessage(113);
	game_time_advance(game_ticks(60 * 60 * 24 * 14));
	set_global_var(GVAR_ENTERING_VATS_HOW, 1);
	load_map(MAP_MBVATS12, 5);
end

procedure Troy03
begin
	set_global_var(GVAR_ENEMY_CATHEDRAL, 1);
	NMessage(114);
	call combat;
end

procedure Troy04
begin
	Reply(115);
	giq_option(4, NAME, 116, Troy02, NEUTRAL_REACTION);
	giq_option(4, NAME, 117, Troy03, BAD_REACTION);
	giq_option(4, NAME, 118, Troy08, BAD_REACTION);
	giq_option(4, NAME, 119, Troy07, BAD_REACTION);
end

procedure Troy05
begin
	Reply(120);
	giq_option(-3, NAME, 121, Troy02, NEUTRAL_REACTION);
	giq_option(-3, NAME, 122, Troy03, NEUTRAL_REACTION);
end

procedure Troy06
begin
	Reply(123);
	giq_option(4, NAME, 124, Troy02, NEUTRAL_REACTION);
	giq_option(4, NAME, 125, Troy03, BAD_REACTION);
	giq_option(4, NAME, 126, Troy07, BAD_REACTION);
end

procedure Troy07
begin
	set_global_var(GVAR_ENEMY_CATHEDRAL, 1);
	BMessage(127);
	call combat;
end

procedure Troy08
begin
	Reply(128);
	giq_option(4, NAME, 129, Troy02, NEUTRAL_REACTION);
	giq_option(4, NAME, 120, Troy03, BAD_REACTION);
	giq_option(4, NAME, 121, Troy07, BAD_REACTION);
end
