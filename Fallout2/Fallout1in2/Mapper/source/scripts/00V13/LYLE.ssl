/*

   V13 - Water thief

*/

#include "..\headers\define.h"
#include "..\headers\vault13.h"

#define NAME                    SCRIPT_LYLE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_V13)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Lyle01;
procedure Lyle02;
procedure Lyle03;
procedure Lyle04;
procedure LyleEnd;

variable night_person;
variable wake_time;
variable sleep_time;
variable home_tile;
variable sleep_tile;

variable tmp_hostile;
variable initial := 0;
variable DeathType := 57;

#define LVAR_Sleeping                     (4)

procedure start begin
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (local_var(5)) then begin
         if (tile_distance_objs(self_obj, dude_obj) < 8) then begin
            call flee_dude;
         end
         else begin
            if (tile_distance(dude_tile, home_tile) > 4) then begin
               if (local_var(4) == 0) then begin
                  self_walk_to_tile(home_tile);
               end
            end
         end
      end
   end
   sleeping
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_local_var(5, 1);
   end
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then
      set_global_var(GVAR_VAULT_13_HOSTILE, 1);

   inc_good_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure map_enter_p_proc begin
   if (v13_invaded) then begin
      invasion_kill_critter;
   end

   set_self_team(TEAM_VAULT13);
   sleep_time := 1920;
   wake_time := 710;
   home_tile := 24911;
   sleep_tile := home_tile;
end

procedure pickup_p_proc
begin
   if ((GAME_TIME_IN_DAYS) >= 30) then begin
      float_msg(self_obj, mstr(101), FLOAT_MSG_NORMAL);
   end
   set_global_var(GVAR_VAULT_13_HOSTILE, 1);
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (local_var(4) == 1) then begin
      float_msg(self_obj, message_str(SCRIPT_GENVAULT, 166), FLOAT_MSG_NORMAL);
   end
   else begin
      if (global_var(GVAR_VAULT_13_HOSTILE) or local_var(5)) then begin
         float_msg(self_obj, mstr(114), FLOAT_MSG_NORMAL);
      end
      else begin
         if (global_var(GVAR_QUEST_VAULT13_4_WATERCHIP) == 2) then begin
            float_msg(self_obj, mstr(102), FLOAT_MSG_NORMAL);
         end
         else begin
            if (global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF) == 2) then begin
               float_msg(self_obj, mstr(113), FLOAT_MSG_NORMAL);
            end
            else begin
               start_gdialog(507, self_obj, 4, -1, -1);
               gsay_start;
               call Lyle01;
               gsay_end;
               end_dialogue;
            end
         end
      end
   end
end

procedure Lyle01 begin
   Reply(mstr(103) + dude_name + mstr(104));
   NOption(105, Lyle02, 4);
   NLowOption(107, LyleEnd);
end

procedure Lyle02 begin
   if ((GAME_TIME_IN_DAYS) < 30) then begin
      NMessage(106);
   end
   else begin
      Reply(108);
      NOption(109, Lyle03, 4);
      GOption(110, Lyle04, 4);
   end
end

procedure Lyle03 begin
   NMessage(111);
end

procedure Lyle04 begin
   set_global_var(GVAR_QUEST_VAULT13_5_WTR_THIEF, 1);
   GMessage(112);

end

procedure LyleEnd begin
end
