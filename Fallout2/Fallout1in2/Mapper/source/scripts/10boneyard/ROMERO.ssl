/*

	Boneyard - Romero, Blades

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\boneyard.h"

#define NAME                    SCRIPT_ROMERO
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;

procedure show_true_name;
procedure show_false_name;
procedure Romero00;
procedure Romero01;
procedure Romero02;
procedure Romero03;
procedure Romero04;
procedure Romero05;
procedure Romero06;
procedure Romero07;
procedure Romero08;
procedure Romero09;
procedure Romero10;
procedure Romero11;
procedure Romero12;
procedure Romero13;
procedure Romero14;
procedure Romero15;
procedure RomeroCombat;
procedure RomeroEnd;
procedure GiveLocket;

variable name;
variable asked;
variable initial := 0;
variable tmp_hostile;

procedure damage_p_proc;

procedure start begin
	if not(initial) then begin
		name := mstr(100);
		set_self_team(TEAM_LA_BLADES);
		set_self_ai(AI_BLADES);
		initial := 1;
	end
end

procedure destroy_p_proc begin
   inc_good_critter
	if (source_obj == dude_obj) then begin
		set_global_var(GVAR_ENEMY_BLADES, 1);
	end
end

procedure look_at_p_proc begin
	call description_p_proc;
end

procedure description_p_proc begin
	if (get_arming_blades or (global_var(GVAR_BLADES_STATUS) == 2)) then begin
		call show_true_name;
	end
	else begin
		if ((dude_luck > 6) or (dude_iq > 6)) then begin
			call show_true_name;
		end
		else if (dude_iq < 4) then begin
			call show_false_name;
		end
		else begin
			if (random(0, 1)) then begin
				call show_true_name;
			end
			else begin
				call show_false_name;
			end
		end
	end
end

procedure talk_p_proc begin
	dude_look_at_self;
	if (dude_iq < 4) then begin
		display_msg(message_str(SCRIPT_GENCHAT, 175));
	end
	else begin
		start_gdialog(285, self_obj, 4, -1, -1);
		gsay_start;
		set_local_var(0, 1);
		if (global_var(GVAR_QUEST_BONEYARD_2_ROMERO) == 3) then begin
			call Romero15;
		end
		else begin
			if ((game_time_hour >= 1900) or (game_time_hour < 600)) then begin
				call Romero06;
			end
			else if (global_var(GVAR_QUEST_BONEYARD_2_ROMERO) == 2) then begin
				call Romero13;
			end
			else if (global_var(GVAR_QUEST_BONEYARD_1_ME_BLADE) == 2) then begin
				if (not(local_var(1))) then begin
					call Romero07;
				end
				else begin
					call Romero14;
				end
			end
			else begin
				call Romero00;
			end
		end
		gsay_end;
		end_dialogue;
	end
end

procedure show_true_name begin
	script_overrides;
	if (local_var(0)) then begin
		display_msg(mstr(100));
	end
	else begin
		display_msg(mstr(103));
	end
end

procedure show_false_name begin
	script_overrides;
	display_msg(mstr(random(104, 110)));
	script_overrides;
end

procedure Romero00 begin
	Reply(111);
	giq_option(4, NAME, 112, Romero01, NEUTRAL_REACTION);
	giq_option(4, NAME, 113, RomeroCombat, NEUTRAL_REACTION);
	giq_option(4, NAME, 114, Romero03, NEUTRAL_REACTION);
	giq_option(4, NAME, 115, RomeroEnd, NEUTRAL_REACTION);
end

procedure Romero01 begin
	Reply(116);
	giq_option(4, NAME, 117, Romero02, NEUTRAL_REACTION);
	giq_option(4, NAME, 118, Romero03, NEUTRAL_REACTION);
	giq_option(4, NAME, 119, Romero03, NEUTRAL_REACTION);
	giq_option(4, NAME, 120, RomeroCombat, NEUTRAL_REACTION);
end

procedure Romero02 begin
	NMessage(121);
end

procedure Romero03 begin
	Reply(122);
	giq_option(4, NAME, 123, Romero04, NEUTRAL_REACTION);
	giq_option(4, NAME, 124, RomeroCombat, NEUTRAL_REACTION);
	giq_option(4, NAME, 125, RomeroEnd, NEUTRAL_REACTION);
end

procedure Romero04 begin
	Reply(126);
	giq_option(4, NAME, 127, Romero05, NEUTRAL_REACTION);
	giq_option(4, NAME, 128, RomeroEnd, NEUTRAL_REACTION);
end

procedure Romero05 begin
	NMessage(129);
end

procedure Romero06 begin
	NMessage(130);
end

procedure Romero07 begin
	set_local_var(1, 1);
	Reply(131);
	giq_option(4, NAME, 132, Romero08, NEUTRAL_REACTION);
	giq_option(4, NAME, 133, Romero09, NEUTRAL_REACTION);
	giq_option(4, NAME, 134, Romero11, NEUTRAL_REACTION);
end

procedure Romero08 begin
	NMessage(135);
	call GiveLocket;
end

procedure Romero09 begin
	Reply(136);
	giq_option(4, NAME, 137, Romero10, NEUTRAL_REACTION);
	giq_option(4, NAME, 138, Romero12, NEUTRAL_REACTION);
	giq_option(4, NAME, 139, Romero11, NEUTRAL_REACTION);
end

procedure Romero10 begin
	Reply(140);
	giq_option(4, NAME, 141, GiveLocket, NEUTRAL_REACTION);
	giq_option(4, NAME, 142, Romero11, NEUTRAL_REACTION);
end

procedure Romero11 begin
	NMessage(143);
end

procedure Romero12 begin
	Reply(144);
	giq_option(4, NAME, 145, Romero11, NEUTRAL_REACTION);
	giq_option(4, NAME, 146, Romero08, NEUTRAL_REACTION);
end

procedure Romero13
begin
	NMessage(147);
	set_global_var(GVAR_QUEST_BONEYARD_2_ROMERO, 3);
end

procedure Romero14 begin
	NMessage(148);
end

procedure Romero15 begin
	NMessage(149);
end

procedure RomeroCombat begin
	set_global_var(GVAR_ENEMY_BLADES, 1);
	attack(dude_obj);
end

procedure RomeroEnd begin
end

procedure GiveLocket begin
	variable Item := 0;
	set_global_var(GVAR_QUEST_BONEYARD_2_ROMERO, 1);

	Item := create_object(PID_GOLD_LOCKET, 0, 0);
	add_obj_to_inven(dude_obj, Item);
end

procedure damage_p_proc begin
	if (source_obj == dude_obj) then begin
		set_global_var(GVAR_ENEMY_BLADES, 1);
	end
end
