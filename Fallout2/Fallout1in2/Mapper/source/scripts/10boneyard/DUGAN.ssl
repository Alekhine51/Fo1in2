/*

   Boneyard - Dugan, Blades member, nuka cola addict

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_DUGAN
#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure look_at_p_proc;
procedure description_p_proc;
procedure talk_p_proc;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;

procedure Dugan01;
procedure Dugan02;
procedure Dugan03;
procedure Dugan04;
procedure Dugan05;
procedure Dugan06;
procedure Dugan07;
procedure Dugan08;
procedure Dugan09;
procedure DuganEnd;

variable Only_Once := 1;
variable PsstTime;

procedure Start begin
   if Only_Once then begin
      set_self_team(TEAM_LA_BLADES);
      set_self_ai(AI_BLADES);
      Only_Once := 0;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   if (local_var(4) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure description_p_proc begin
   script_overrides;
   if (local_var(4) == 1) then begin
      display_msg(mstr(101));
   end
   else begin
      display_msg(mstr(100));
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
      float_msg(self_obj, message_str(SCRIPT_ENEMY, random(100, 105)), FLOAT_MSG_RED);
   end
   else begin
      if ((local_var(4) == 1) and (dude_iq < 4)) then begin
         float_msg(self_obj, mstr(120), FLOAT_MSG_NORMAL);
      end
      else begin
         get_reaction
         start_dialog_at_node(Dugan01);
      end
   end
end

procedure critter_p_proc begin
   if ((((game_time / 10) - PsstTime) >= 30) and (tile_distance_objs(self_obj, dude_obj) <= 10)) then begin
      float_msg(self_obj, mstr(random(121, 125)), FLOAT_MSG_NORMAL);
      PsstTime := game_time / 10;
   end
   if (self_can_see_dude) then begin
      if (global_var(GVAR_ENEMY_BLADES) == 1) then begin
         attack(dude_obj);
      end
   end
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_BLADES, 1);
   end
end

procedure pickup_p_proc begin
   set_global_var(GVAR_ENEMY_BLADES, 1);
end

procedure Dugan01 begin
   Reply(102);
   giq_option(4, NAME, 103, Dugan02, NEUTRAL_REACTION);
   giq_option(4, NAME, 104, Dugan03, NEUTRAL_REACTION);
   giq_option(4, NAME, 105, Dugan05, NEUTRAL_REACTION);
   if (global_var(GVAR_BLADES_STATUS) < 1) then begin
      giq_option(4, NAME, 106, Dugan08, NEUTRAL_REACTION);
   end
   giq_option(4, NAME, 107, DuganEnd, NEUTRAL_REACTION);
   giq_option(-3, NAME, 108, Dugan09, NEUTRAL_REACTION);
end

procedure Dugan02 begin
   NMessage(109);
end

procedure Dugan03 begin
   Reply(110);
   call Dugan04;
end

procedure Dugan04 begin
   gsay_option(NAME, 111, Dugan05, 50);
   gsay_option(NAME, 112, Dugan06, 50);
   gsay_option(NAME, 113, Dugan07, 50);
   gsay_option(NAME, 114, Dugan01, 50);
   gsay_option(NAME, 115, DuganEnd, 50);
end

procedure Dugan05 begin
   Reply(116);
   call Dugan04;
end

procedure Dugan06 begin
   Reply(117);
   call Dugan04;
end

procedure Dugan07 begin
   Reply(118);
   call Dugan04;
end

procedure Dugan08 begin
   NMessage(119);
   call Dugan01;
end

procedure Dugan09 begin
   NMessage(120);
end

procedure DuganEnd begin
end
