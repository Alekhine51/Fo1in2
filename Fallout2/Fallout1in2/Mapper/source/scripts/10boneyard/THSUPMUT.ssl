/*

   Boneyard - Super Mutant invaders

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\maps\lafollwr.h"
#include "..\headers\sfall\main.h"

#define NAME                    SCRIPT_THSUPMUT
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_BONEYARD)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure timed_event_p_proc;
procedure combat_p_proc;

procedure THSupMut0;
procedure THSupMut1;
procedure THSupMut2;
procedure THSupMut3;
procedure THSupMut4;
procedure THSupMut5;
procedure THSupMut6;
procedure THSupMut7;
procedure THSupMutend;

procedure combat;
procedure to_orfeo;

#define SetSelfFlag(x)     set_flags(self_obj, x)
#define GetSelfFlag        get_flags(self_obj)

#define set_ghost          SetSelfFlag(GetSelfFlag bwor FLAG_TRANSGLASS)
#define rem_ghost          SetSelfFlag(GetSelfFlag bwand bwnot(FLAG_TRANSGLASS))

#define self_is_nightkin   ((self_pid == PID_NIGHTKIN) or (self_pid == PID_NIGHTKIN_GUARD) or                                          \
                           (self_pid == PID_NIGHTKIN_GUARD_2) or (self_pid == PID_TOUGH_NIGHTKIN) or                                   \
                           (self_pid == PID_DEADLY_NIGHTKIN) or (self_pid == PID_SUPER_NIGHTKIN) or (self_pid == PID_MASTER_NIGHTKIN))

#define spawn_stealth_boy  if self_is_nightkin and (random(0, 2) == 1) then begin   \
                              Item := create_object(PID_STEALTH_BOY, 0, 0);         \
                              add_mult_objs_to_inven(self_obj, Item, 1);            \
                           end

#define LVAR_Here_Before      (0)
#define LVAR_AssignName       (1)

import variable Orfeo_ptr;

variable rndx;
variable tmp_hostile;
variable Only_Once := 1;
variable in_timed_event;
variable weaponPtr;
variable Item;
variable Use_Stealth_Boy := 0;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_SGT);
      if (local_var(LVAR_AssignName) == 0) then begin
         set_local_var(LVAR_AssignName, (100 + map_var(MVAR_COC_THMUT_NAME)));
         inc_map_var(MVAR_COC_THMUT_NAME);

         weaponPtr := create_object(PID_LASER_RIFLE, 0, 0);
         add_obj_to_inven(self_obj, weaponPtr);
         wield_obj_critter(self_obj, weaponPtr);
         add_obj_to_inven(self_obj, create_object(PID_MICRO_FUSION_CELL, 0, 0));

         spawn_stealth_boy
      end
      self_rotate(random(0, 5));
   end
end

procedure map_enter_p_proc begin
   if (map_first_run) then
      spawn_stealth_boy
end

procedure critter_p_proc begin
   if (global_var(GVAR_ENEMY_FOLL_INVADERS)) then begin
      tmp_hostile := 1;
   end
   else if (tmp_hostile and self_can_see_dude) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (not(in_timed_event)) then begin
         in_timed_event := 1;
         add_timer_event(self_obj, game_ticks(random(6, 10)), random(1, 3));
      end
      if (self_can_see_dude and (tile_distance_objs(self_obj, dude_obj) < 6)) then begin
         if (local_var(LVAR_Here_Before) == 0) then begin
            dialogue_system_enter;
         end
      end
   end
end

procedure destroy_p_proc begin
   if (source_obj == dude_obj) then begin
      set_global_var(GVAR_ENEMY_FOLL_INVADERS, 1);
   end
   inc_evil_critter
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(local_var(LVAR_AssignName)));
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   script_overrides;
   dude_look_at_self;
   if (local_var(LVAR_Here_Before)) then begin
      start_dialog_at_node(THSupMut7);
   end
   else begin
      start_dialog_at_node(THSupMut0);
   end
end

procedure timed_event_p_proc begin
   if (fixed_param == 1) then begin
      self_rotate(random(0, 5));
   end
   else begin
      self_walk_to_tile(tile_num_in_direction(self_tile, random(0, 5), random(1, 3)));
   end
   in_timed_event := 0;
end

procedure combat_p_proc begin
   if (self_is_nightkin and self_item_count(PID_STEALTH_BOY)) then begin
      if (fixed_param == COMBAT_SUBTYPE_TURN) then begin
         if (Use_Stealth_Boy == 0) then begin
            Use_Stealth_Boy := 1;

            //TODO: Add sfx for activation?
            anim(self_obj, ANIM_magic_hands_middle, -1);
            set_ghost;

            display_msg(self_name + gen_mstr(190));
         end
      end

      //--- Always add transparency when in combat!
      if (Use_Stealth_Boy == 1) then
         set_ghost;
   end
end

procedure THSupMut0 begin
   set_local_var(LVAR_Here_Before, 1);
   Reply(110);
   NLowOption(111, THSupMut1);
   NOption(112, to_orfeo, 4);
   NOption(113, THSupMut2, 4);
   NOption(114, THSupMut3, 6);
end

procedure THSupMut1 begin
   NMessage(115);
   call to_orfeo;
end

procedure THSupMut2 begin
   NMessage(116);
   call to_orfeo;
end

procedure THSupMut3 begin
   Reply(117);
   NOption(118, THSupMut4, 6);
   NOption(119, THSupMut6, 6);
end

procedure THSupMut4 begin
   Reply(120);
   NOption(121, to_orfeo, 6);
   NOption(122, combat, 6);
   NOption(123, THSupMut5, 8);
end

procedure THSupMut5 begin
   Reply(124);
   NOption(125, to_orfeo, 8);
   NOption(126, combat, 8);
end

procedure THSupMut6 begin
   NMessage(127);
   call combat;
end

procedure THSupMut7 begin
   NMessage(128);
   call to_orfeo;
end

procedure combat begin
   tmp_hostile := 1;
end

procedure THSupMutend begin
end

procedure to_orfeo begin
   load_map("lafollwr.map", 11);
end
