/*

   Scrapheap - Computer terminal

      Can play Fallout on this one.

*/

/* Include Files */
#include "define.h"
#include "scrapheap.h"
#include "maps/junkdemo.h"

#define NAME                    SCRIPT_DEMOCOMP

#include "command.h"

/* Standard Script Procedures */
procedure start;
procedure description_p_proc;
procedure look_at_p_proc;
procedure use_p_proc;
procedure talk_p_proc;

procedure fix;
procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node999;

variable test;

procedure start begin
end

procedure description_p_proc begin
   script_overrides;
   display_msg(message_str(NAME, 100));
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(message_str(NAME, 100));
end

procedure use_p_proc begin
   script_overrides;
   if (map_var(MVAR_Broken_Gen) != 1) then begin
      if (local_var(4) == 1) then begin
         dialogue_system_enter;
      end
      else begin
         call fix();
      end
   end
end

procedure talk_p_proc begin
   start_gdialog(-1, self_obj, 4, -1, -1);
   gsay_start;
   call Node001();
   gsay_end;
   end_dialogue;
end

procedure fix begin
   if (has_skill(dude_obj, 12) > has_skill(dude_obj, 13)) then begin
      test := roll_vs_skill(dude_obj, 12, 0);
   end
   else begin
      test := roll_vs_skill(dude_obj, 13, 0);
   end
   if (is_success(test)) then begin
      display_msg(message_str(NAME, 101));
      set_local_var(4, 1);
   end
   else begin
      display_msg(message_str(NAME, 103));
      if (is_critical(test)) then begin
         display_msg(message_str(NAME, 104));
         critter_dmg(dude_obj, 1, 0 bwor 256);
      end
   end
end

procedure Node001 begin
   gsay_reply(NAME, 105);
   giq_option(4, NAME, 106, Node002, 50);
   giq_option(4, NAME, 107, Node999, 50);
end

procedure Node002 begin
   gsay_reply(NAME, 108);
   giq_option(4, NAME, 109, Node003, 50);
   giq_option(4, NAME, 110, Node999, 50);
end

procedure Node003 begin
   gsay_reply(NAME, 111);
   giq_option(4, NAME, 112, Node004, 50);
   giq_option(4, NAME, 113, Node005, 50);
   giq_option(4, NAME, 114, Node999, 50);
end

procedure Node004 begin
   gsay_reply(NAME, 115);
   giq_option(4, NAME, 116, Node003, 50);
end

procedure Node005 begin
   if (not(local_var(5))) then begin
      give_exp_points(100);
      display_msg(message_str(NAME, 119));
      set_local_var(5, 1);
   end
   float_msg(dude_obj, message_str(NAME, 118), 0);
end

procedure Node999 begin
end
