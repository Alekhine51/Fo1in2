/*

   Hub - Rufus - cut-content

      NOTE:

         Can be found in Hub Oldtown now.

*/

/* Include Files */
#include "define.h"
//#include "area/hub.h"

#define NAME                    SCRIPT_RUFUS
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_SKAG);
      set_self_ai(AI_HUB_PEASANTS);
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure talk_p_proc begin
   dude_look_at_self;

   if tmp_hostile then begin
      floater(101);
   end
   else begin
      floater_rand(102, 108);
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

