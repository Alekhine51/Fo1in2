/*

   Hub - Deadguy

*/

/* Include Files */
#include "..\headers\define.h"
//#include "..\headers\necropolis.h"

#define NAME                    SCRIPT_DEADGUY
#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;

procedure Deadguy00;
procedure Deadguy01;
procedure Deadguy02;
procedure Deadguy03;
procedure Deadguy04;
procedure Deadguy05;
procedure Deadguy06;
procedure Deadguy07;
procedure Deadguy08;
procedure DeadguyEnd;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_HUB_SKAG);
      set_self_ai(AI_HUB_PEASANTS);
   end
end

procedure combat
begin
   tmp_hostile := 1;
end

procedure critter_p_proc
begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc
begin
   if (source_obj == dude_obj) then begin
      tmp_hostile := 1;
   end
end

procedure talk_p_proc
begin
   dude_look_at_self;
   get_reaction
   if (local_var(4) == 1) then begin
      call Deadguy02;
   end
   else begin
      set_local_var(4, 1);
      start_gdialog(564, self_obj, 4, -1, -1);
      gsay_start;
      call Deadguy00;
      gsay_end;
      end_dialogue;
   end
end

procedure destroy_p_proc begin
   inc_good_critter
end

procedure look_at_p_proc
begin
   script_overrides;
   display_msg(mstr(100));
end

procedure Deadguy00
begin
   Reply(101);
   BOption(102, Deadguy01, 4);
   if (local_var(5) == 0) then begin
      NOption(103, Deadguy03, 4);
   end
   NOption(104, Deadguy07, 4);
   NLowOption(105, Deadguy06);
end

procedure Deadguy01
begin
   NMessage(106);
end

procedure Deadguy02
begin
   float_msg(self_obj, mstr(107), FLOAT_MSG_RED);
end

procedure Deadguy03
begin
   set_local_var(5, 1);
   dude_caps_adjust(20);
   Reply(108);
   NOption(109, DeadguyEnd, 4);
   NOption(110, Deadguy04, 4);
end

procedure Deadguy04
begin
   Reply(111);
   NOption(112, Deadguy05, 4);
   NOption(113, DeadguyEnd, 4);
end

procedure Deadguy05
begin
   if (dude_is_male) then begin
      NMessage(114);
   end
   else begin
      NMessage(115);
   end
end

procedure Deadguy06
begin
   if (dude_is_male) then begin
      NMessage(118);
   end
   else begin
      NMessage(119);
   end
end

procedure Deadguy07
begin
   if (dude_is_male) then begin
      Reply(122);
   end
   else begin
      Reply(123);
   end
   NOption(126, Deadguy08, 4);
   NOption(127, combat, 4);
end

procedure Deadguy08
begin
   if (dude_is_male) then begin
      NMessage(128);
   end
   else begin
      NMessage(129);
   end
end

procedure DeadguyEnd
begin
end
