#include "..\headers\define.h"

#define NAME                    SCRIPT_FALCON1

#include "..\headers\command.h"
#include "..\headers\modreact.h"

procedure start;
procedure combat;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure Falcon00;
procedure Falcon01;
procedure Falcon02;
procedure Falcon03;
procedure Falcon04;
procedure Falcon05;
procedure Falcon05a;
procedure Falcon06;
procedure Falcon06a;
procedure Falcon07;
procedure Falcon07a;
procedure Falcon09;
procedure Falcon11;
procedure Falcon14;
procedure Falcon15;
procedure Falcon16;
procedure FalconEnd;

variable tmp_hostile;
variable Only_Once := 1;
variable First_Water;
variable Money_Pack;
variable Money_Count;
variable item;

procedure start begin
	if Only_Once then begin
		Only_Once := 0;
		set_self_team(TEAM_HUB_UNDERGROUND);
		set_self_ai(AI_MERCHANT);
	end
end

procedure combat begin
	tmp_hostile := 1;
end

procedure critter_p_proc begin
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
end

procedure pickup_p_proc
begin
	if (source_obj == dude_obj) then begin
		tmp_hostile := 1;
	end
end

procedure talk_p_proc
begin
	dude_look_at_self;
	get_reaction
	start_gdialog(697, self_obj, 4, -1, -1);
	gsay_start;
	call Falcon00;
	gsay_end;
	end_dialogue;
end

procedure destroy_p_proc begin
	inc_good_critter
end

procedure look_at_p_proc
begin
	script_overrides;
	display_msg(mstr(100));
end

procedure Falcon00
begin
	Reply(101);
	giq_option(4, NAME, 102, Falcon05, NEUTRAL_REACTION);
	giq_option(4, NAME, 103, Falcon06, NEUTRAL_REACTION);
	giq_option(4, NAME, 104, Falcon07, NEUTRAL_REACTION);
	giq_option(4, NAME, 106, Falcon04, NEUTRAL_REACTION);
	giq_option(-3, NAME, 107, Falcon03, NEUTRAL_REACTION);
end

procedure Falcon01
begin
	NMessage(108);
end

procedure Falcon02
begin
	NMessage(109);
end

procedure Falcon03
begin
	if (dude_is_male) then begin
		NMessage(110);
	end
	else begin
		NMessage(111);
	end
end

procedure Falcon04
begin
	NMessage(112);
end

procedure Falcon05
begin
	Reply(113);
	giq_option(4, NAME, 114, Falcon05a, NEUTRAL_REACTION);
	giq_option(4, NAME, 115, Falcon11, NEUTRAL_REACTION);
end

procedure Falcon05a
begin
	if (dude_caps >= 2) then begin
		call Falcon09;
	end
	else begin
		call Falcon01;
	end
end

procedure Falcon06
begin
	Reply(116);
	giq_option(4, NAME, 117, Falcon06a, NEUTRAL_REACTION);
	giq_option(4, NAME, 115, Falcon11, NEUTRAL_REACTION);
end

procedure Falcon06a
begin
	if (dude_caps < 5) then begin
		call Falcon01;
	end
	else begin
		call Falcon14;
	end
end

procedure Falcon07 begin
	Reply(118);
	giq_option(4, NAME, 117, Falcon07a, NEUTRAL_REACTION);
	giq_option(4, NAME, 115, Falcon11, NEUTRAL_REACTION);
end

procedure Falcon07a begin
	if (dude_caps < 10) then begin
		call Falcon01;
	end
	else begin
		call Falcon16;
	end
end

procedure Falcon09
begin
	if (First_Water) then begin
		Reply(mstr(139) + mstr(140) + mstr(141));
	end
	else begin
		Reply(mstr(136) + mstr(137) + mstr(138));
	end
	Money_Pack := dude_item(PID_BOTTLE_CAPS);
	Money_Count := rm_mult_objs_from_inven(dude_obj, Money_Pack, 2);
	destroy_object(Money_Pack);
	First_Water := 1;
	call Falcon15;
end

procedure Falcon11
begin
	if (random(0, 1)) then begin
		NMessage(mstr(151) + mstr(152));
	end
	else begin
		NMessage(mstr(153) + mstr(154));
	end
end

procedure Falcon14
begin
	Money_Pack := dude_item(PID_BOTTLE_CAPS);
	Money_Count := rm_mult_objs_from_inven(dude_obj, Money_Pack, 5);
	destroy_object(Money_Pack);
	item := create_object(PID_BEER, 0, 0);
	add_obj_to_inven(dude_obj, item);
	Reply(mstr(160) + mstr(161));
	call Falcon15;
end

procedure Falcon15
begin
	giq_option(4, NAME, 102, Falcon05, NEUTRAL_REACTION);
	giq_option(4, NAME, 103, Falcon06, NEUTRAL_REACTION);
	giq_option(4, NAME, 104, Falcon07, NEUTRAL_REACTION);
	giq_option(4, NAME, 106, Falcon04, NEUTRAL_REACTION);
	giq_option(-3, NAME, 107, Falcon03, NEUTRAL_REACTION);
end

procedure Falcon16
begin
	Money_Pack := dude_item(PID_BOTTLE_CAPS);
	Money_Count := rm_mult_objs_from_inven(dude_obj, Money_Pack, 10);
	destroy_object(Money_Pack);
	item := create_object(PID_BOOZE, 0, 0);
	add_obj_to_inven(dude_obj, item);
	Reply(mstr(163) + mstr(164));
	call Falcon15;
end

procedure FalconEnd
begin
end
