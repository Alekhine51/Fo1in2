/*

   Should armor get destroyed on critter death?

*/

/* Include Files */
#include "define.h"
#include "command.h"

#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;
procedure combatdamage_handler;
procedure deathanim_handler;
procedure ondeath_handler;

variable worn_armor;

procedure start begin
   if (game_loaded) then begin
      if fo1in2_destroy_armor_enabled then begin
         register_hook_proc(HOOK_COMBATDAMAGE, combatdamage_handler);
         register_hook_proc(HOOK_DEATHANIM1, deathanim_handler);
         register_hook_proc(HOOK_ONDEATH, ondeath_handler);
      end
   end
end

procedure combatdamage_handler begin
   variable target := get_sfall_arg;
   worn_armor := critter_inven_obj(target, INVEN_TYPE_WORN);
end

procedure deathanim_handler begin
   variable item, critter := get_sfall_arg_at(2);

   if (critter == dude_obj orElse obj_in_party(critter)) then return;

   item := critter_inven_obj(critter, INVEN_TYPE_WORN);
   if (item andAlso obj_item_subtype(item) == item_type_armor) then begin
      rm_obj_from_inven(critter, worn_armor);
      worn_armor = 0;
      debug_msg("Destroying critter armor!");
   end
end

procedure ondeath_handler begin
   variable item, critter := get_sfall_arg;

   if (critter == dude_obj orElse obj_in_party(critter)) then return;

   if (worn_armor andAlso obj_item_subtype(worn_armor) == item_type_armor) then begin
      rm_obj_from_inven(critter, worn_armor);
      debug_msg("Destroying critter armor!");
   end
   worn_armor = 0;
end
