/*

   This script changes the survivalist perk to give + 20% to outdoorsman on every rank (3).
   In Fo2, the perk would only give + 25% _once_ (1 rank only).

*/

/* Include Files */
#include "define.h"
#include "command.h"
#include "sfall/main.h"

/* Standard Script Procedures */
procedure start;

procedure GameModeChange_handler;
procedure survivalist_perk;

variable prev_mode := 0;

procedure start begin
   if (game_loaded) then begin
      set_global_script_type(1);
      register_hook_proc(HOOK_GAMEMODECHANGE, GameModeChange_handler);
   end

   if (get_game_mode bwand CHARSCREEN) then begin
      prev_mode := CHARSCREEN;
      call survivalist_perk;
   end
   else if (prev_mode == CHARSCREEN) then begin
      prev_mode := 0;
      set_global_script_repeat(0);
      set_sfall_global("survperk", dude_perk(PERK_survivalist));
   end
end

procedure GameModeChange_handler begin
   if (get_game_mode bwand CHARSCREEN) then begin
      set_global_script_repeat(1);
   end
end

procedure survivalist_perk begin
   if (dude_perk(PERK_survivalist) > get_sfall_global_int("survperk")) then begin
      set_sfall_global("survperk", get_sfall_global_int("survperk") + 1);
      critter_mod_skill(dude_obj, SKILL_OUTDOORSMAN, 20);
      display_stats; // We have to refresh the interface
   end
end
