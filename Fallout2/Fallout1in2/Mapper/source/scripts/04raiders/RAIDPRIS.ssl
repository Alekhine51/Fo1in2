/*

   Raiders - Prisoner (cut-content)

*/

/* Include Files */
#include "define.h"
#include "area/shadysands.h"

#define NAME                    SCRIPT_RAIDPRIS
#define TOWN_REP_VAR            (GVAR_TOWN_REP_RAIDERS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure talk_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;

procedure prisoner00;
procedure prisoner01;
procedure prisoner02;
procedure prisoner03;
procedure prisoner04;
procedure prisonerend;
procedure Critter_Action;

#define LVAR_Herebefore       (4)

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   if (shady_invaded and CUR_MAP_RAIDERS) then begin
      invasion_kill_critter;
      kill_critter(self_obj, DeathType);
   end
   else begin
      if Only_Once then begin
         Only_Once := 0;
         set_self_team(TEAM_PRISONERS);
         set_self_ai(AI_GENERIC_RAIDER);
      end
   end
end

procedure map_enter_p_proc begin
   // Cut content, remove for now:
   destroy_object(self_obj);
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(message_str(NAME, 100));
end

procedure critter_p_proc begin
   call Critter_Action;
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure talk_p_proc begin
   dude_look_at_self;
   get_reaction

   if (local_var(LVAR_Herebefore)) then begin
      start_dialog_at_node(prisoner03);
   end
   else begin
      set_local_var(LVAR_Herebefore, 1);
      start_dialog_at_node(prisoner00);
   end
end

procedure prisoner00 begin
   gsay_reply(NAME, 101);
   NOption(102, prisoner01, 4);
   NOption(103, prisoner02, 6);
   NLowOption(104, prisoner01);
end

procedure prisoner01 begin
   set_local_var(LVAR_Herebefore, 1);
   gsay_message(NAME, 105, NEUTRAL_REACTION);
end

procedure prisoner02 begin
   gsay_reply(NAME, 106);
   NOption(107, prisonerend, 4);
   NOption(108, prisonerend, 4);
end

procedure prisoner03 begin
   gsay_message(NAME, 109, NEUTRAL_REACTION);
end

procedure prisoner04 begin
   gsay_message(NAME, 110, NEUTRAL_REACTION);
end

procedure prisonerend begin
end

procedure Critter_Action begin
   if get_tandi_returned then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
   else begin
      if global_var(GVAR_TANDI_ESCAPE) then begin
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
      if (map_var(2) == 1) then begin
         set_global_var(GVAR_ENEMY_RAIDERS, 1);
      end
   end
   if (tile_distance_objs(self_obj, dude_obj) > 12) then begin
      tmp_hostile := 0;
   end
end

procedure damage_p_proc begin
   tmp_hostile := 1;
   if (global_var(GVAR_MISTAKEN_ID) == 0) then begin
      set_global_var(GVAR_ENEMY_RAIDERS, 1);
   end
end

procedure destroy_p_proc begin
   inc_inv_good_crit
end
