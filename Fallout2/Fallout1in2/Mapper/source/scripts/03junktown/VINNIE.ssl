/*

	Junktown - Vinnie, Skullz Leader

*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\junktown.h"

#define NAME                    SCRIPT_VINNIE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_JUNKTOWN)

#include "..\headers\command.h"
#include "..\headers\modreact.h"

/* Standard Script Procedures */
procedure start;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure map_enter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;

procedure Vinnie01;
procedure Vinnie01a;
procedure Vinnie02;
procedure Vinnie03;
procedure Vinnie04;
procedure Vinnie05;
procedure Vinnie06;
procedure Vinnie07;
procedure Vinnie08;
procedure Vinnie09;
procedure Vinnie10;
procedure Vinnie14;
procedure Vinnie14a;
procedure Vinnie15;
procedure Vinnie15a;
procedure Vinnie16;
procedure Vinnie17;
procedure Vinnie18;
procedure Vinnie19;
procedure Vinnie20;
procedure Vinnie21;
procedure Vinnie22;
procedure Vinnie23;
procedure Vinnie24;
procedure Vinnie25;
procedure VinnieEnd;

procedure VinnieCombat;
procedure VinnieKillNeal;
procedure set_sleep_time;

import variable Neal_ptr;
import variable removal_ptr;

variable tmp_hostile;
variable line160flag;
variable line162flag;
variable Vinnie_kill_Neal;
variable moving_disabled;
variable sleeping_disabled;
variable sleep_time;
variable wake_time;
variable night_person;

procedure start begin
end

procedure timed_event_p_proc begin
	if (fixed_param == 1) then begin
		game_ui_enable;
		if (map_var(1) != 0) then begin
			set_global_var( GVAR_DUDE_ENEMY_SKULZ, 1 );
			float_msg( self_obj, message_str(NAME, 162), FLOAT_MSG_RED );
			attack(dude_obj);
		end
		else begin
			attack( Neal_ptr );
		end
	end
end

procedure critter_p_proc begin
	if (global_var( GVAR_DUDE_ENEMY_SKULZ ) == 1) then begin
		if (self_can_see_dude) then begin
			attack(dude_obj);
		end
	end
	if (tmp_hostile) then begin
		tmp_hostile := 0;
		attack(dude_obj);
	end
	else begin
		if (CUR_MAP_JUNKCSNO) and (line160flag == 0) then begin //  JUNKTOWN - CASINO, GIZMO, SKUM PITT, BOXING, DOGMEAT
			float_msg( self_obj, message_str(NAME, 160), FLOAT_MSG_RED );
			game_ui_disable;
			add_timer_event(self_obj, game_ticks(2), 1);
			line160flag := 1;
		end
	end
end

procedure destroy_p_proc begin
	if (CUR_MAP_JUNKCSNO) then begin //  JUNKTOWN - CASINO, GIZMO, SKUM PITT, BOXING, DOGMEAT
		set_map_var(0, (map_var(0) - 1));
	end
	rm_timer_event(self_obj);

	if (source_obj == dude_obj) then begin
		set_global_var( GVAR_DUDE_ENEMY_SKULZ, 1 );
	end

	set_quest_join_skullz_completed;

	inc_evil_critter
	rm_timer_event(self_obj);
end

procedure look_at_p_proc begin
	script_overrides;
	if (local_var(0)) then begin
		display_msg(message_str(NAME, 100));
	end
	else begin
		display_msg(message_str(NAME, 101));
	end
end

procedure map_enter_p_proc begin
	call set_sleep_time;
	set_self_team(TEAM_JUNKTOWN_SKULZ );
	set_self_ai( AI_SKULZ );
	if ((global_var( GVAR_BUST_SKULZ ) == 2) or ((CUR_MAP_JUNKCSNO) and (global_var( GVAR_IS_NEAL_DEAD ) == 1))) then begin
		move_to(self_obj, 7000, 0);
		if not(is_loading_game) then set_self_invisible;
		removal_ptr := self_obj;
	end
end

procedure pickup_p_proc begin
	tmp_hostile := 1;
end

procedure talk_p_proc begin
	dude_look_at_self;

	if (global_var( GVAR_DESTROY_VATS_13 ) == 2) then begin
		float_msg( self_obj, message_str(NAME, 171), FLOAT_MSG_BLUE );
	end
	else if (global_var( GVAR_IS_NEAL_DEAD ) == 1) then begin
		float_msg( self_obj, message_str(NAME, 170), FLOAT_MSG_RED );
	end
	else begin
		start_gdialog(385, self_obj, 4, -1, -1);
		gsay_start;
		if (global_var( GVAR_DESTROY_VATS_13 ) == 1) then begin
			call Vinnie24;
		end
		else if (local_var(2) == 1) then begin
			call Vinnie19;
		end
		else if (local_var(0) == 1) then begin
			call Vinnie10;
		end
		else begin
			call Vinnie01;
		end
		gsay_end;
		end_dialogue;
	end

	if (local_var(1) == 1) then begin
		display_msg(message_str(NAME, 163));
		give_exp_points(400);
		set_global_var( GVAR_PLAYER_REPUTATION, (check_general_rep - 1) );
		set_local_var(1, 2);
		if (global_var( GVAR_IS_NEAL_DEAD ) == 0) then begin
			set_global_var( GVAR_DESTROY_VATS_11, (GAME_TIME_IN_DAYS) + 2 );
		end
	end

	if (Vinnie_kill_Neal) then begin
		Vinnie_kill_Neal := 0;
		load_map(MAP_JUNKCSNO, 7);
	end

	if ((global_var( GVAR_DESTROY_VATS_11 ) > (GAME_TIME_IN_DAYS)) and (global_var( GVAR_IS_NEAL_DEAD ) == 0)) then begin
		float_msg( self_obj, message_str(NAME, 159), FLOAT_MSG_BLUE );
	end
end

procedure Vinnie01 begin
	set_local_var(0, 1);
	gsay_reply( NAME, 103 );
	giq_option( 4, NAME, 104, Vinnie02, NEUTRAL_REACTION );
	giq_option( 4, NAME, 105, Vinnie03, NEUTRAL_REACTION );
	if quest_join_skullz_active then begin
		giq_option( 4, NAME, 102, Vinnie01a, NEUTRAL_REACTION );
	end
	giq_option( -3, NAME, 106, Vinnie04, NEUTRAL_REACTION );
end

procedure Vinnie01a begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Vinnie14;
	end
	else begin
		call Vinnie15;
	end
end

procedure Vinnie02 begin
	gsay_message( NAME, 107, BAD_REACTION );
end

procedure Vinnie03 begin
	gsay_reply( NAME, 108 );
	giq_option( 4, NAME, 109, VinnieEnd, NEUTRAL_REACTION );
	giq_option( 4, NAME, 110, Vinnie06, NEUTRAL_REACTION );
	giq_option( 6, NAME, 111, Vinnie05, NEUTRAL_REACTION );
end

procedure Vinnie04 begin
	gsay_message( NAME, 112, NEUTRAL_REACTION );
end

procedure Vinnie05 begin
	gsay_reply( NAME, 113 );
	giq_option( 4, NAME, 114, VinnieEnd, NEUTRAL_REACTION );
	giq_option( 6, NAME, 115, Vinnie07, NEUTRAL_REACTION );
end

procedure Vinnie06 begin
	gsay_message( NAME, 116, BAD_REACTION );
	call VinnieCombat;
end

procedure Vinnie07 begin
	gsay_reply( NAME, 117 );
	giq_option( 4, NAME, 118, VinnieEnd, NEUTRAL_REACTION );
	giq_option( 6, NAME, 119, Vinnie08, NEUTRAL_REACTION );
end

procedure Vinnie08 begin
	gsay_reply( NAME, 120 );
	giq_option( 4, NAME, 121, VinnieEnd, NEUTRAL_REACTION );
	if (global_var( GVAR_HIRED_BY_GIZMO )) then begin
		giq_option( 6, NAME, 122, Vinnie09, NEUTRAL_REACTION );
	end
end

procedure Vinnie09 begin
	gsay_message( NAME, 123, NEUTRAL_REACTION );
end

procedure Vinnie10 begin
	gsay_reply( NAME, 126 );
	giq_option( 4, NAME, 127, VinnieEnd, NEUTRAL_REACTION );
	if quest_join_skullz_active then begin
		giq_option( 4, NAME, 102, Vinnie01a, NEUTRAL_REACTION );
	end
end

procedure Vinnie14 begin
	gsay_reply( NAME, 131 );
	if (has_rep_berserker) then begin
		giq_option( 4, NAME, 132, VinnieCombat, BAD_REACTION );
	end
	giq_option( 4, NAME, 133, Vinnie16, NEUTRAL_REACTION );
	giq_option( 4, NAME, 134, Vinnie14a, NEUTRAL_REACTION );
end

procedure Vinnie14a begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Vinnie16;
	end
	else begin
		call Vinnie15;
	end
end

procedure Vinnie15 begin
	gsay_reply( NAME, 135 );
	giq_option( 4, NAME, 136, Vinnie15a, NEUTRAL_REACTION );
	if (has_rep_berserker) then begin
		giq_option( 4, NAME, 137, VinnieCombat, BAD_REACTION );
	end
	giq_option( 4, NAME, 138, VinnieEnd, NEUTRAL_REACTION );
	giq_option( 4, NAME, 139, Vinnie03, NEUTRAL_REACTION );
end

procedure Vinnie15a begin
	if (is_success(roll_vs_skill(dude_obj, SKILL_SPEECH, 0))) then begin
		call Vinnie16;
	end
	else begin
		call Vinnie04;
	end
end

procedure Vinnie16 begin
	gsay_reply( NAME, 140 );
	if (global_var( GVAR_IS_NEAL_DEAD ) == 1) then begin
		giq_option( 4, NAME, 145, Vinnie22, GOOD_REACTION );
	end
	else begin
		if (global_var( GVAR_DESTROY_VATS_14 ) == 1) then begin
			giq_option( 4, NAME, 144, Vinnie19, GOOD_REACTION );
		end
		else begin
			giq_option( 4, NAME, 141, Vinnie17, NEUTRAL_REACTION );
		end
	end
	giq_option( 4, NAME, 142, Vinnie18, BAD_REACTION );
	giq_option( 4, NAME, 143, Vinnie04, BAD_REACTION );
end

procedure Vinnie17 begin
	set_local_var(2, 1);
	gsay_message( NAME, 146, GOOD_REACTION );
end

procedure Vinnie18 begin
	gsay_message( NAME, 147, BAD_REACTION );
end

procedure Vinnie19 begin
	gsay_reply( NAME, 148 );
	if (dude_item_count( PID_URN )) then begin
		giq_option( 4, NAME, 149, Vinnie20, GOOD_REACTION );
	end
	else begin
		giq_option( 4, NAME, 150, Vinnie21, BAD_REACTION );
	end
	if (global_var( GVAR_IS_NEAL_DEAD ) == 1) then begin
		giq_option( 4, NAME, 151, Vinnie22, GOOD_REACTION );
	end
end

procedure Vinnie20 begin
	set_local_var(2, 0);
	if (local_var(1) == 0) then begin
		set_local_var(1, 1);
	end
	if (global_var( GVAR_IS_NEAL_DEAD ) == 1) then begin
		call Vinnie22;
	end
	else begin
		set_global_var( GVAR_DESTROY_VATS_11, (GAME_TIME_IN_DAYS) + 2 );
		gsay_reply( NAME, 152 );
		giq_option( 4, NAME, 153, Vinnie06, BAD_REACTION );
		giq_option( 4, NAME, 154, VinnieKillNeal, GOOD_REACTION );
		giq_option( 5, NAME, 155, Vinnie23, NEUTRAL_REACTION );
	end
end

procedure Vinnie21 begin
	gsay_message( NAME, 156, BAD_REACTION );
end

// Dude becomes a skull
procedure Vinnie22 begin
   set_quest_join_skullz_completed;

	set_local_var(2, 0);
	set_global_var( GVAR_DESTROY_VATS_13, 2 );
	gsay_message( NAME, 157, GOOD_REACTION );
end

procedure Vinnie23 begin
	set_global_var( GVAR_DESTROY_VATS_13, 1 );
	gsay_message( NAME, 158, NEUTRAL_REACTION );
end

procedure Vinnie24 begin
	gsay_reply( NAME, 165 );
	giq_option( 4, NAME, 166, VinnieKillNeal, GOOD_REACTION );
	giq_option( 4, NAME, 167, Vinnie23, NEUTRAL_REACTION );
	giq_option( 4, NAME, 168, Vinnie25, BAD_REACTION );
end

procedure Vinnie25 begin
	gsay_message( NAME, 169, BAD_REACTION );
	call VinnieCombat;
end

procedure VinnieEnd begin
end

procedure VinnieCombat begin
	tmp_hostile := 1;
end

procedure VinnieKillNeal begin
	set_global_var( GVAR_DESTROY_VATS_13, 2 );
	Vinnie_kill_Neal := 1;
end

procedure set_sleep_time begin
	if (CUR_MAP_JUNKKILL) then begin //  JUNKTOWN - KILLIAN, CRASH HOUSE
		sleep_time := random(2200, 2215);
		wake_time := random(800, 815);
	end
	else if (CUR_MAP_JUNKCSNO) then begin //  JUNKTOWN - CASINO, GIZMO, SKUM PITT, BOXING, DOGMEAT
		sleep_time := 300;
		wake_time := 1600;
		night_person := 1;
	end
	else if (CUR_MAP_JUNKENT) then begin //  JUNKTOWN - ENTRANCE, MORBID, LARS
		sleep_time := 2200;
		wake_time := 1000;
	end
end
